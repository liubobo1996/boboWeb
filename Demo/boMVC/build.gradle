// 当前项目构建的配置文件

// 加入插件
plugins {
    id 'java'
    // 替换 gradle 内置依赖管理, 使用 spring 项目本身的 maven 依赖文件
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
//    id 'war'
    id 'idea'
}

group = 'kybmig'
// 发布第一个版本
version = '1.0'
// Linux 服务器上的 JDK 版本
sourceCompatibility = '11'

jar {
    // 配置 manifest 文件
    manifest {
        // 主函数入口
        attributes 'Main-Class': 'boMVC.Server'
    }

    dependsOn configurations.runtimeClasspath
    from {
        // 收集依赖代码 (打包第三方库)
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

// 编译输出显示中文
compileJava.options.encoding = "UTF-8"
compileTestJava.options.encoding = "UTF-8"

// 加入 Maven 仓库
repositories {
    maven { url 'https://maven.aliyun.com/repository/public/' }
    maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
    maven { url 'https://maven.aliyun.com/repository/google' }
    maven { url 'https://maven.aliyun.com/repository/spring' }
    maven { url 'https://maven.aliyun.com/repository/spring-plugin' }
    maven { url 'https://maven.aliyun.com/repository/central' }
    mavenLocal()
    mavenCentral()
}

configurations {
    // 编译后热更新配置
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
}

// 加入依赖和第三方库 更新后点击右上角小象图标
dependencies {
    compile group: 'com.alibaba', name: 'fastjson', version: '1.2.70'
    // https://mvnrepository.com/artifact/org.freemarker/freemarker
    // 现在都用 implementation， compile 弃用
    implementation group: 'org.freemarker', name: 'freemarker', version: '2.3.30'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// 让 idea 的输出目录和 gradle 一致, 这样 idea 运行的时候就能利用 gradle 的编译, 加速编译和运行速度
idea{
    module{
        inheritOutputDirs = false
        outputDir = compileJava.destinationDir
        testOutputDir = compileTestJava.destinationDir
    }
}

